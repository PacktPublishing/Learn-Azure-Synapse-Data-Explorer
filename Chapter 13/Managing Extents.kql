// 13.1 - Reviewing the database merge policy

.show database ['drone-telemetry'] policy merge

// 13.2 - Adding tags at ingestion time

// 13.3 - we will create a destination table first to perform data ingestion.  

.create table ['fleet data with tags'] (
    DeviceData: string, 
    LocalDateTime: string, 
    DeviceState: string, 
    CoreTemp: long, 
    CoreStatus: string, 
    Compass: long, 
    Altitude: long, 
    Speed: long, 
    PayloadWeight: long, 
    EventData: string
) 

// 13.4 - Now, we'll perform ingestion with tags. 

.ingest into table ['fleet data with tags']
    'https://dronetelemetrydatalake.blob.core.windows.net/raw-data/drone-telemetry-simplified.csv?sp=r&st=2022-12-23T12:35:01Z&se=2022-12-30T20:35:01Z&spr=https&sv=2021-06-08&sr=c&sig=8OFOuVnfha0%2BqhDF2eIqfNJlm5xl33BGDTmnxXjxTGc%3D' 
    with (ignoreFirstRecord=true, tags='["drop-by:2023-03-23"]') 

// 13.5 - Altering tags on all extents of a table

.alter extent tags ('Drone telemetry data. Do not delete.') <| .show table ['fleet data with tags'] extents

// 13.6 - Adding tags on all extents of a table

.alter-merge extent tags ('Data source: telemetry data lake.') <| .show table ['fleet data with tags'] extents

// 13.7 - Reviewing extent tags

.show table ['fleet data with tags'] extents
| project ExtentId, Tags

// Moving extents. 
// 13.8 - We'll create a new table first. 

.create table ['fleet data with tags - archive'] (
    DeviceData: string, 
    LocalDateTime: string, 
    DeviceState: string, 
    CoreTemp: long, 
    CoreStatus: string, 
    Compass: long, 
    Altitude: long, 
    Speed: long, 
    PayloadWeight: long, 
    EventData: string
) 

// 13.9 - Now we'll move all extents from one table to the other.

.move extents all from table ['fleet data with tags'] to table ['fleet data with tags - archive']

// 13.10 - Move specific extents.

.move extents from table ['fleet data with tags - archive'] to table ['fleet data with tags'] (ee5ae751-9a79-488a-acd8-ccf58b315479)

// 13.11 - Move extents based on their tags.

.move extents to table ['fleet data with tags - archive'] <| .show table ['fleet data with tags'] extents | where Tags == 'drop-by:2023-03-23'

// 13.12 - Dropping extents

.drop extent 49aa2250-d8b8-4127-9049-739399e2a89f

// 13.13 - Dropping extents after a certain age

.drop extents <| .show database ['drone-telemetry'] extents | where MaxCreatedOn < now() - time(90d)

// 13.14 - Checking what would happen if I ran the .drop exents command without acctually running it. 

.drop extents whatif  <| .show database ['drone-telemetry'] extents | where MaxCreatedOn < now() - time(90d)