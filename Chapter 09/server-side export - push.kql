// 09.2 - Export to cloud storage

.export async compressed to csv 
    (
        h@"https://<storageaccountname>.blob.core.windows.net/data-exports?<SASKeyHere>" 
    ) with (
        namePrefix="DeviceError-",
        includeHeaders="all",
        encoding ="UTF8NoBOM"
    )
  <| ['fleet data'] | where  DeviceState == 'DeviceError' | project DeviceData, CoreTemp

// 09.3 - Export to a SQL table

  .export to sql fleet_data
    h@"Server=tcp:<AzureSQLInstanceName>.database.windows.net,1433;Database=<DatabaseName>;Authentication=Active Directory Integrated;Connection Timeout=30;"
        with (createifnotexists="true")
    <| ['fleet data'] | where LocalDateTime between (datetime(2021-11-01) .. datetime(2021-11-02))

// 09.4 - Export to an external table. 
// First create the table... 

.create external table TopPayloads (DeviceData:string, LocalDateTime:datetime, PayloadWeight:long)  
kind=storage 
dataformat=csv 
( 
   h@"https://<storageaccountname>.blob.core.windows.net/data-exports?<SASKeyHere>" 
) 

// 09.5 - ... and now perform the data export task... 

.export to table TopPayloads 
<| ['fleet data'] | top 10000 by PayloadWeight desc | project DeviceData, LocalDateTime, PayloadWeight

// 09.6 - ... and then verifying that it worked. 

external_table('TopPayloads')
| take 10

// 09.7 - Configuring continuous data ingestion. 

.create-or-alter continuous-export ContinuousTelemetryData
to table TopPayloads
with
(intervalBetweenRuns=2h)
<| 
['fleet data'] 
| project DeviceData, LocalDateTime, PayloadWeight

// 09.8 -  show the job details. 

.show continuous-export ContinuousTelemetryData 
| project ExternalTableName, IntervalBetweenRuns, IsDisabled, LastRunResult, IsRunning

// 09.9 -  disabling the continuous data export job: 
 
 .disable continuous-export ContinuousTelemetryData
